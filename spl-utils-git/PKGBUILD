# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>
# See http://github.com/demizer/archzfs for build scripts.

pkgname="spl-utils-git"
pkgver=0.6.2.r28.g3ceb71e_3.14.0_4
pkgrel=1

# Used incase the i686 and x86_64 linux packages get out of sync with the
# PKGREL. This occurred on January 31, 2014 where i686 was versioned at
# 3.12.9-1 and x86_64 was versioned at 3.12.9-2.
_kernel_version_x32_clean="3.14.0_4"
_kernel_version_x64_clean="3.14.0_4"

[[ $CARCH == "i686" ]] && _kernel_version_clean=${_kernel_version_x32_clean} | \
    _kernel_version_clean=${_kernel_version_x64_clean}

pkgdesc="Solaris Porting Layer kernel module support files."
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("git+https://github.com/zfsonlinux/spl.git"
        "spl-utils.hostid")
groups=("archzfs-git")
md5sums=('SKIP'
         'a54f0041a9e15b050f25c463f1db7449')
license=("GPL")
provides=("spl-utils")
conflicts=("spl-utils")

pkgver() {
    cd "${srcdir}/spl"

    # cutting off 'spl-' prefix that presents in the git tag
    echo $(git describe --long | \
        sed -r 's/^spl-//;s/([^-]*-g)/r\1/;s/-/./g')_${_kernel_version_clean}
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh

    _at_enable=""
    [ "${CARCH}" == "i686"  ] && _at_enable="--enable-atomic-spinlocks"

    ./configure --prefix=/usr \
                --libdir=/usr/lib \
                --sbindir=/usr/bin \
                --with-config=user \
                ${_at_enable}

    make
}

package() {
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install

    # install -D -m644 "${srcdir}"/spl-utils.hostid "${pkgdir}"/etc/hostid
}
