# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>
# See http://github.com/demizer/archzfs for build scripts.

pkgname="zfs-utils-git"
pkgver=0.6.2_r254_g0b75bdb_3.14.1_1
pkgrel=1

# Used incase the i686 and x86_64 linux packages get out of sync with the
# PKGREL. This occurred on January 31, 2014 where i686 was versioned at
# 3.12.9-1 and x86_64 was versioned at 3.12.9-2.
_kernel_version_x32_clean="3.14.1_1"
_kernel_version_x64_clean="3.14.1_1"

[[ $CARCH == "i686" ]] && _kernel_version_clean=${_kernel_version_x32_clean} | \
    _kernel_version_clean=${_kernel_version_x64_clean}

pkgdesc="Kernel module support files for the Zettabyte File System."
depends=("spl-git=0.6.2_r33_g89aa970_3.14.1_1")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(git+https://github.com/zfsonlinux/zfs.git
        zfs-utils.bash-completion-r1
        zfs-utils.initcpio.install
        zfs-utils.initcpio.hook)
groups=("archzfs")
md5sums=('SKIP'
         '9ddb0c8a94861f929d0fa741fdc49950'
         'ebea2b1e593a9a19efa0f7ae7451bbc7'
         'b567a95a34759f196bf4be8061a895e5')
license=("CDDL")

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-config=user
    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install

    # Remove uneeded files
    rm -r "${pkgdir}"/etc/init.d
    rm -r "${pkgdir}"/usr/lib/dracut

    # move module tree /lib -> /usr/lib
    cp -r "${pkgdir}"/{lib,usr}
    rm -r "${pkgdir}"/lib

    # Fixup path
    mv "${pkgdir}"/sbin/mount.zfs "${pkgdir}"/usr/bin/
    rm -r "${pkgdir}"/sbin

    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
}
