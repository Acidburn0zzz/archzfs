# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('zfs')
pkgver=0.6.0_rc13_3.7.3
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
depends=('spl=0.6.0_rc13_3.7.3' 'zfs-utils=0.6.0_rc13_3.7.3' 'linux=3.7.3')
makedepends=('linux-headers=3.7.3')
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.0-rc13.tar.gz)
groups=('archzfs')
md5sums=('0d166fe162d1c5d0ca49e16d89f05c81')
license=('CDDL')
install=zfs.install

build() {
  cd ${srcdir}/zfs-0.6.0-rc13
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc13 \
              --with-config=kernel
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc13
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib
}
