# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('zfs')
pkgver=0.6.0_rc14_3.7.5
pkgrel=2
pkgdesc="Kernel modules for the Zettabyte File System."
depends=('spl=0.6.0_rc14_3.7.5' 'zfs-utils=0.6.0_rc14_3.7.5' 'linux=3.7.5')
makedepends=('linux-headers=3.7.5')
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.0-rc14.tar.gz)
groups=('archzfs')
md5sums=('adf09eb2144ef49734be12565d9ea4fe')
license=('CDDL')
install=zfs.install

build() {
  cd ${srcdir}/zfs-0.6.0-rc14
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc14 \
              --with-config=kernel
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc14
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib
}
