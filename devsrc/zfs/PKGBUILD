# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('zfs')
pkgver=0.6.0_rc12
pkgrel=7
pkgdesc="Kernel modules for the Zettabyte File System."
depends=('spl=0.6.0_rc12' 'zfs-utils=0.6.0_rc12' 'linux=3.6.10')
makedepends=('linux-headers=3.6.10')
arch=('x86_64')
url='http://zfsonlinux.org/'
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-0.6.0-rc12.tar.gz zfs.install)
groups=('archzfs')
md5sums=('6524f2c20fe9ad33a97879981e5c9a45' '7ac100ebe01cc26da63a06effb2c0405')
license=('CDDL')
install=zfs.install

build() {
  cd ${srcdir}/zfs-0.6.0-rc12
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc12 \
              --with-config=kernel
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc12
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib
}
