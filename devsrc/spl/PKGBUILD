# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('spl')
pkgver=0.6.0_rc14_3.7.10
pkgrel=3
pkgdesc='Solaris Porting Layer kernel modules.'
depends=('spl-utils=0.6.0_rc14_3.7.10' 'linux=3.7.10')
makedepends=('linux-headers=3.7.10')
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-0.6.0-rc14.tar.gz)
groups=('archzfs')
md5sums=('07b6cf56659a95bc22ac45dbdd8344a0')
license=('GPL')
install=spl.install

build() {
  cd ${srcdir}/spl-0.6.0-rc14
  ./autogen.sh
  if [[ $CARCH == i686 ]]; then
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/sbin \
                --with-config=kernel --enable-atomic-spinlocks
  else
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/sbin \
                --with-config=kernel
  fi
  make
}

package() {
  cd ${srcdir}/spl-0.6.0-rc14
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib
}
