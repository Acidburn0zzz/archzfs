# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('zfs-utils')
pkgver=0.6.0_rc14_3.7.10
pkgrel=2
pkgdesc="Kernel module support files for the Zettabyte File System."
depends=('spl=0.6.0_rc14_3.7.10' 'linux=3.7.10')
makedepends=('linux-headers=3.7.10')
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.0-rc14.tar.gz
        zfs-utils.bash-completion
        zfs-utils.initcpio.install
        zfs-utils.initcpio.hook
        zfs-utils.service)
groups=('archzfs')
md5sums=('adf09eb2144ef49734be12565d9ea4fe'
         '3e1c4a29c4f7d590e6a3041f2c61d6ff'
         'd654e88a606fe68f93a8fa26bca4655d'
         '105fe46115c6fc6a335399c74bd58289'
         '161e6a5f5f314c9308b4a4565c01fe45')
license=('CDDL')

build() {
  cd ${srcdir}/zfs-0.6.0-rc14
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc14 \
              --with-config=user
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc14
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib

  # Remove uneeded directories
  rm -r ${pkgdir}/usr/share/dracut
  rm -r ${pkgdir}/etc/rc.d

  install -D -m644 ${srcdir}/zfs-utils.initcpio.hook ${pkgdir}/usr/lib/initcpio/hooks/zfs
  install -D -m644 ${srcdir}/zfs-utils.initcpio.install ${pkgdir}/usr/lib/initcpio/install/zfs
  install -D -m644 ${srcdir}/zfs-utils.service ${pkgdir}/usr/lib/systemd/system/zfs.service
  install -D -m644 ${srcdir}/zfs-utils.bash-completion ${pkgdir}/usr/share/bash-completion/completions/zfs
}
